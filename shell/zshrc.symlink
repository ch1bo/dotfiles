# Generic .zshrc which sources all *.zsh files in the dotfiles repository (completion.zsh last)
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# add each topic folder to fpath so that they can add functions and completion scripts
for topic_folder ($DOTFILES/*) if [ -d $topic_folder ]; then
  fpath=($topic_folder $fpath)
fi

# source zsh specific config files
for file in ${(M)config_files:#*/shell/*.zsh}; do
  source $file
done
config_files=(${config_files:#*/shell/*.zsh})

# source everything but the completion files
for file in ${config_files:#*/completion.zsh}; do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -Uz compinit
compinit -i

# source completion files after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source $file
done

unset config_files

# syntax highlighting
local prefix=$(nix-build -Q '<nixpkgs>' -A zsh-syntax-highlighting)
source "${prefix}/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"
unset prefix

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [ -e ${HOME}/.localrc ]; then
  source ${HOME}/.localrc
fi
